plugins {
    id "com.jfrog.bintray" version "1.7.1"
    id "com.gradle.plugin-publish" version "0.10.0"
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: "com.gradle.plugin-publish"

wrapper {
    gradleVersion = '3.0'
}

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

group = 'net.robyf'
archivesBaseName = 'gradle-jarinjar-plugin'

ext.majorVersion = 1
ext.minorVersion = 0

def computeVersionNumber() {
    def computedVersion
    ext.env = System.getenv()
    ext.branchName = env.BRANCH_NAME
    if (ext.branchName == "master") {
        ext.buildNumber = env.BUILD_NUMBER?.toInteger()
        computedVersion = "${majorVersion}.${minorVersion}.${buildNumber}"
    } else {
        computedVersion = "${majorVersion}.${minorVersion}-SNAPSHOT"
    }
    return computedVersion
}

version = computeVersionNumber()
ext.isSnapshot = version.endsWith("SNAPSHOT")

if ( isSnapshot ) {
    println "Using snapshot repository for artifact uploads"
    ext.mavenCentralUploadUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
} else {
    println "Using staging repository for artifact uploads"
    ext.mavenCentralUploadUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
}

configurations {
    archives
}

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    testCompile 'junit:junit:4.12'
}

task sourceJar(type: Jar) {
    description = "An archive of the source code for Maven Central"
    classifier = 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar) {
    description = "An archive of the GroovyDocs for Maven Central"
    classifier = 'javadoc'
    from groovydoc
}

artifacts {
    archives jar, groovydocJar, sourceJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    if (gradle.startParameter.taskNames.contains("uploadArchives")) {
        repositories {
            mavenDeployer {
                if (signing.signatory) {
                    beforeDeployment {
                        signing.signPom(it)
                    }
                }
                configuration = configurations.archives
                repository(url: mavenCentralUploadUrl) {
                    authentication(userName: mavenUsername, password: mavenPassword)
                    releases(updatePolicy: "always")
                    snapshots(updatePolicy: "always")
                }
                pom.project(pomConfiguration)
            }
        }
    }
}

def getPomConfiguration() {
    return {
        name 'Gradle JarInJar Plugin'
        packaging 'jar'
        description 'A Gradle plugin for generating self-contained executable jars.'
        url 'https://github.com/robyf/gradle-jarinjar-plugin'
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'robyf'
                name 'Roberto Fasciolo'
                email 'rob@robyf.net'
            }
        }
        scm {
            connection 'scm:https://robyf@github.com/robyf/gradle-jarinjar-plugin'
            developerConnection 'scm:git@github.com:robyf/gradle-jarinjar-plugin.git'
            url 'https://github.com/robyf/gradle-jarinjar-plugin'
        }
    }
}

bintray {
    user = bintrayUsername
    key = bintrayApiKey

    configurations = [ 'archives' ]

    pkg {
        repo = 'maven-repo'
        name = 'gradle-jarinjar-plugin'
        desc = 'A Gradle plugin for generating self-contained executable jar files.'
        licenses = [ 'Apache-2.0' ]
        websiteUrl = 'https://github.com/robyf/gradle-jarinjar-plugin'
        issueTrackerUrl = 'https://github.com/robyf/gradle-jarinjar-plugin/issues'
        vcsUrl = 'https://github.com/robyf/gradle-jarinjar-plugin.git'
        labels = [ 'gradle', 'jarinjar' ]
        publicDownloadNumbers = true
        version {
            name = version
            // vcsTag = version
            attributes = [
                'gradle-plugin': 'net.robyf.gradle.jarinjar:net.robyf:gradle-jarinjar-plugin'
            ]
        }
    }
}

pluginBundle {
    website = 'https://github.com/robyf/gradle-jarinjar-plugin'
    vcsUrl = 'https://github.com/robyf/gradle-jarinjar-plugin.git'
    description = 'A Gradle plugin for generating self-contained executable jar files.<br><a href="https://github-com/robyf/gradle-jarinjar-plugin">https://github-com/robyf/gradle-jarinjar-plugin</a>'
    tags = [ 'gradle', 'jarinjar' ]

    plugins {
        gradleLiquibasePlugin {
            id = 'net.robyf.jarinjar'
            displayName = 'Gradle Jar in jar plugin'
        }
    }
}
